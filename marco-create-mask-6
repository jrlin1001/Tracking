//script for creating mask and measure for live imaging (NIC)
//Jerry 2014/04/24

for (site=1;site<21;site++)
{

//----------------------------------------------------------------
start = getTime;
while (nImages>0) { 
          selectImage(nImages); 
          close(); 
}

start = getTime;
if (isOpen("ROI Manager")) { 
      selectWindow("ROI Manager"); 
      run("Close"); 
} 
setBatchMode(true); 

//--open image sequence and duplicate--
run("Image Sequence...", "open=c:\\NIC_Training\\FOXO_002_w1RFP_s1_t1.TIF number=22241 starting=1 increment=1 scale=100 file=w1RFP_s"+site+"_ sort");
run("Subtract Background...", "rolling=50 stack");
setOption("BlackBackground", false);
rename("original");
run("Duplicate...", "title=mask duplicate range=1-139");
//---------------------------------------------------
print("Site ",site,"Building Stacks",(getTime-start)/1000); 


//--processing to binary mask--
selectWindow("mask");
run("Gaussian Blur...", "sigma=2 stack");
run("Enhance Contrast...", "saturated=0.5");
run("Make Binary", "method=Mean background=Default calculate black");
run("Invert", "stack");
run("Watershed", "stack");
run("Invert", "stack");
rename("nuclear-mask");
//------------------------------------
print("Site",site,"Convert to Mask",(getTime-start)/1000); 

//--analysis particle---
selectWindow("nuclear-mask");
run("Set Scale...", "distance=0 known=0 pixel=1 unit=pixel global");
run("Analyze Particles...", "size=100-2000 circularity=0.10-1.00 show=Masks clear include add stack");
rename("mask");
run("Clear Results");
selectWindow("mask");
close();
//selectWindow("nuclear-mask");
//close();
//----------------------------
print("Site",site,"Creating Mask",(getTime-start)/1000); 

//--Measure--
selectWindow("original");
run("Set Measurements...", "area mean standard min centroid perimeter shape median area_fraction stack add redirect=None decimal=3");
roiManager("Measure");
saveAs("Results", "C:\\tracking_NIC\\measure-mask-s"+site+".csv");
//----------------
close();
roiManager("Show None");
run("Clear Results");
print("Site",site,"Measuring Mask",(getTime-start)/1000);
 

//--Measure 2nd channel---
run("Image Sequence...","open=c:\\NIC_Training\\FOXO_002_w1RFP_s1_t1.TIF number=22241 starting=1 increment=1 scale=100 file=w2YFP_s"+site+"_ sort");
run("Subtract Background...", "rolling=50 stack");

roiManager("Show None"); 
counts=roiManager("count");
for(i=0; i<counts; i++) {
    roiManager("Select", i);
    run("Measure");
    run("Enlarge...", "enlarge=4");
    run("Measure");
}
saveAs("Results", "C:\\tracking_NIC\\measure-cytosol-s"+site+".csv");
run("Clear Results");

setBatchMode(false); 

print("Site",site,"Final measure",(getTime-start)/1000); 

//-----------------------------------

}


